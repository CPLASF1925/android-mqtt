apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.11"

    // MQTT
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.0'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
}


apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = rootProject.ext.version
group = rootProject.ext.groupId

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    // 读取配置文件中的用户名和key
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        userOrg = 'fungo'
        repo = "maven"                      // 你在bintray上创建的库的名称
        name = rootProject.ext.name         // 在jcenter中的项目名称
        websiteUrl = rootProject.ext.siteUrl
        vcsUrl = rootProject.ext.gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            groupId rootProject.ext.groupId
            artifactId 'mqtt-android'
            packaging 'aar'
            name rootProject.ext.name
            url rootProject.ext.siteUrl
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'     // 开源协议名称
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt' // 协议地址
                }
            }
            developers {
                developer {
                    id 'pingerx'
                    name 'pingerx'
                    email 'xmpinger@gmail.com'
                }
            }
            scm {
                connection rootProject.ext.gitUrl
                developerConnection rootProject.ext.gitUrl
                url rootProject.ext.siteUrl
            }
        }
    }
}

// gradlew build
// gradlew install
// gradlew bintrayUpload